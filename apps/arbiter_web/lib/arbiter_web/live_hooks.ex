defmodule ArbiterWeb.LiveHooks do
  @moduledoc """
  Shared `on_mount` callbacks attached via `live_session` in the router.

  ## `:current_path`

  Stores the request path of the current LiveView on the socket as
  `:current_path`, kept in sync across `live_navigate`/`live_patch` via a
  `handle_params` hook. The `Layouts.app` nav reads it to highlight the
  active link.

  ## `:live`

  Assigns `:live` on the socket as `connected?(socket)` — `false` on the
  initial dead render, `true` once the LiveView process is connected over
  the socket. `Layouts.app`'s navbar badge (and every page-level
  `live_badge`) reads this assign; `live_badge/1`'s `live` attr is
  `required: true` precisely so no call site can fall back to a client-only
  mechanism instead. The DOM-only join-direction mechanism `live_badge/1`
  used to default to (`phx-connected` flipping "stale" to "Live" with no
  server assign) was the root cause of bd-akygjy — see the "Root cause"
  note on `ArbiterWeb.CoreComponents.Feedback.live_badge/1` for what was
  and wasn't established about why. This hook is the single source of
  truth every call site must be fed from.

  ## `:quota`

  Loads the latest quota snapshot for every tracked provider on the default
  workspace and assigns the list as `:quotas` on the socket (`[]` when
  nothing has been captured yet).

  **Temporary:** Codex is filtered from the quota list pending a fix: its
  dispatch is broken (bd-1nyedk, bd-dcvo3n, bd-bi5t54), and showing quota
  bars for a broken provider implies it's dispatchable when it isn't. Once
  dispatch is fixed, remove it from @hidden_providers and this comment.

  The upstream Gemini CLI (`gemini_cli`) used to be hidden here too
  (bd-5r6cdy); the provider itself is gone now (bd-ac53wz).

  Antigravity was hidden here too (bd-5r6cdy: quota was only checkable while the
  app was open, and its token staled ~1h after it closed). The `agy` CLI
  `/usage` probe (`Arbiter.Quota.CloudCode`) superseded that stored-token path,
  so it is shown again (bd-gukyy1); a reading `agy` couldn't refresh carries a
  `message` and renders muted as stale rather than as a current figure.

  ## `:loopback`

  Assigns `:loopback?` — whether the browser on the other end is on this box.
  It is the input to §10.4's one auth rule, and one surface needs it: the
  session dock, whose terminal rides a `/session` socket that trusts a loopback
  peer and nothing else (bd-2zskbb). Off loopback its window says so, and says
  how to get in (an SSH port-forward), rather than mounting a pane that
  silently never attaches.

  It has to be a *root* view's hook. `get_connect_info/2` is root-and-mount
  only, and `ArbiterWeb.SessionDockLive` is a nested, sticky child — so the
  dock is handed the answer through `live_render(..., session: ...)` in
  `layouts/live.html.heex` rather than reading it itself.

  ## `:coordinator_inbox`

  Lifted off `BoardLive` (bd-3kgb0e) so the coordinator's mailbox — the
  upward channel of `arb inbox` / `arb msg` — surfaces from the AppShell
  drawer on every screen instead of only the board. Subscribes to every
  workspace's message topic and assigns `:coordinator_inbox` (unread) and
  `:coordinator_outstanding_count` (seen but not cleared) same as the old
  `BoardLive.refresh_coordinator_inbox/1`. Also owns the drawer's two
  actions (`coordinator_mark_read`, `coordinator_clear`) via a
  `:handle_event` hook, so no LiveView needs its own clauses for them —
  distinct event names from `WorkerDetailLive`'s own `"mark_read"` (a
  different, per-worker mailbox) avoid a collision.
  """

  import Phoenix.Component, only: [assign: 3]
  import Phoenix.LiveView, only: [attach_hook: 4, connected?: 1]

  alias Arbiter.Messages.Message

  require Logger

  # Providers hidden from the UI pending fix; see module docstring for
  # context. Derived from `Arbiter.Quota.hidden_providers/0` (bd-4p6pw7
  # round 2, finding 2) rather than duplicated by hand, so the two can't
  # drift apart.
  @hidden_providers Arbiter.Quota.hidden_providers()

  @coordinator_ref Message.coordinator_ref()

  # bd-8akewg: the drawer is the operator's own view, so it reads and clears as
  # the shared sessionless coordinator reader — the same identity `arb inbox`
  # and a plain minted token use. That reader's state is mirrored onto the
  # message row, so the drawer behaves exactly as it did before per-reader
  # state existed; what changed is that a browser *session's* polling no longer
  # empties it.
  @reader_opts [reader: Message.coordinator_reader()]

  def on_mount(:current_path, _params, _session, socket) do
    socket =
      socket
      |> assign(:current_path, nil)
      |> attach_hook(:gt_current_path, :handle_params, fn _params, uri, socket ->
        {:cont, assign(socket, :current_path, URI.parse(uri).path)}
      end)

    {:cont, socket}
  end

  def on_mount(:live, _params, _session, socket) do
    {:cont, assign(socket, :live, connected?(socket))}
  end

  def on_mount(:loopback, _params, _session, socket) do
    loopback? =
      case Phoenix.LiveView.get_connect_info(socket, :peer_data) do
        %{address: address} -> ArbiterWeb.Loopback.loopback?(address)
        # No `:peer_data` means no transport to ask — a dead render under
        # `Phoenix.ConnTest` with none put on the conn. Treating that as
        # loopback keeps the default the same one the dashboard runs under.
        _ -> true
      end

    {:cont, assign(socket, :loopback?, loopback?)}
  end

  def on_mount(:quota, _params, _session, socket) do
    case Arbiter.Quota.default_workspace_id() do
      {:ok, ws_id} ->
        # `:exclude_providers` drops a hidden provider's view before it's
        # decorated with spend (bd-4p6pw7), rather than filtering the fully
        # decorated list after the fact.
        #
        # The whole decorated result is memoized by `QuotaCache` (bd-4p6pw7
        # round 2, finding 1): `list_latest_for_workspace/2` still reads a
        # `Workspace`, its provider accounts and their workspace links even
        # with `SpendCache` covering the ledger scans, so an uncached mount
        # was still 18-20 queries. `QuotaCache` is only used here — the
        # direct callers (`GET /api/quota`, `arb quota`) read immediately
        # after a write in their own tests and stay uncached so that holds.
        opts = [exclude_providers: @hidden_providers]

        quotas =
          Arbiter.Quota.QuotaCache.fetch(ws_id, opts, fn ->
            Arbiter.Quota.list_latest_for_workspace(ws_id, opts)
          end)

        socket =
          socket
          |> assign(:quotas, quotas)
          |> assign(:_quota_workspace_id, ws_id)
          |> maybe_subscribe_quota(ws_id)

        {:cont, socket}

      _ ->
        {:cont, assign(socket, :quotas, [])}
    end
  end

  def on_mount(:coordinator_inbox, _params, _session, socket) do
    socket =
      socket
      |> assign(:coordinator_inbox_now, DateTime.utc_now())
      |> refresh_coordinator_inbox()
      |> maybe_subscribe_coordinator_inbox()
      |> maybe_tick_coordinator_inbox_now()
      |> attach_hook(:coordinator_inbox_events, :handle_event, fn
        "coordinator_mark_read", %{"id" => id}, socket ->
          with {:ok, msg} <- Ash.get(Message, id),
               {:ok, _} <- Message.mark_read(msg, @reader_opts) do
            {:halt, refresh_coordinator_inbox(socket)}
          else
            _ -> {:halt, refresh_coordinator_inbox(socket)}
          end

        "coordinator_clear", _params, socket ->
          _ = Message.clear_read(@coordinator_ref, @reader_opts)
          {:halt, refresh_coordinator_inbox(socket)}

        _event, _params, socket ->
          {:cont, socket}
      end)

    {:cont, socket}
  end

  # Independent of any host LiveView's own :tick — the drawer is global chrome,
  # so its "N ago" timestamps need to advance even on pages with no clock of
  # their own. A per-minute cadence is plenty for coarse relative labels.
  defp maybe_tick_coordinator_inbox_now(socket) do
    if connected?(socket) do
      :timer.send_interval(60_000, self(), :coordinator_inbox_tick)

      attach_hook(socket, :coordinator_inbox_tick, :handle_info, fn
        :coordinator_inbox_tick, socket ->
          {:halt, assign(socket, :coordinator_inbox_now, DateTime.utc_now())}

        _msg, socket ->
          {:cont, socket}
      end)
    else
      socket
    end
  end

  defp maybe_subscribe_coordinator_inbox(socket) do
    if connected?(socket) do
      workspaces =
        try do
          Arbiter.Tasks.Workspace |> Ash.read!()
        rescue
          _ -> []
        end

      for ws <- workspaces, do: Phoenix.PubSub.subscribe(Arbiter.PubSub, Message.topic(ws.id))

      attach_hook(socket, :coordinator_inbox_updates, :handle_info, fn
        {:new_message, _message}, socket -> {:cont, refresh_coordinator_inbox(socket)}
        {:message_read, _message}, socket -> {:cont, refresh_coordinator_inbox(socket)}
        {:mailbox_cleared, _workspace_id}, socket -> {:cont, refresh_coordinator_inbox(socket)}
        _msg, socket -> {:cont, socket}
      end)
    else
      socket
    end
  end

  # Two figures, not one: pending (unread — never seen) and outstanding (seen
  # but not cleared — still owes an action). Reading no longer empties the
  # queue, so "still open" needs its own number.
  defp refresh_coordinator_inbox(socket) do
    {inbox, outstanding} =
      try do
        {Message.inbox(@coordinator_ref, @reader_opts),
         Message.outstanding(@coordinator_ref, @reader_opts)}
      rescue
        _ -> {[], []}
      end

    socket
    |> assign(:coordinator_inbox, inbox)
    |> assign(:coordinator_outstanding_count, length(outstanding))
  end

  defp maybe_subscribe_quota(socket, workspace_id) do
    if connected?(socket) do
      Phoenix.PubSub.subscribe(Arbiter.PubSub, "quota:#{workspace_id}")

      attach_hook(socket, :quota_updates, :handle_info, fn msg, socket ->
        case msg do
          {:quota_updated, ^workspace_id, quota} ->
            # Skip updates for hidden providers to prevent re-introduction via PubSub
            # Pre-existing nesting 4 — baselined when bd-4x2yhq first
            # wired Credo up. Thresholds stay at the tool's own default so new
            # code is held to it; see the note in .credo.exs.
            # credo:disable-for-next-line Credo.Check.Refactor.Nesting
            if quota.provider in @hidden_providers do
              {:halt, socket}
            else
              {:halt, assign(socket, :quotas, upsert_quota(socket.assigns.quotas, quota))}
            end

          _ ->
            {:cont, socket}
        end
      end)
    else
      socket
    end
  end

  # Replace the list entry matching `quota.provider`, or append it when this
  # is the first snapshot seen for that provider.
  defp upsert_quota(quotas, quota) do
    if Enum.any?(quotas, &(&1.provider == quota.provider)) do
      Enum.map(quotas, fn
        %{provider: provider} = existing when provider == quota.provider ->
          preserve_cost(existing, quota)

        existing ->
          existing
      end)
    else
      quotas ++ [quota]
    end
  end

  # Live broadcast views don't carry `cost_usd` (it's a read-path add-on from the
  # usage ledger, not part of the per-provider fetch), so a naive replace would
  # blank the figure on every tick. Keep the last known cost when the incoming
  # update omits it (bd-ajh7bd). Same for `gate_policy` (bd-clzkvp), which
  # `list_latest_for_workspace/2` adds: without it the bars would fall back to
  # the install-default thresholds on the first tick.
  defp preserve_cost(existing, incoming) do
    incoming
    |> keep_existing(existing, :cost_usd)
    |> keep_existing(existing, :gate_policy)
  end

  defp keep_existing(incoming, existing, key) do
    case Map.get(incoming, key) do
      nil -> Map.put(incoming, key, Map.get(existing, key))
      _ -> incoming
    end
  end
end
