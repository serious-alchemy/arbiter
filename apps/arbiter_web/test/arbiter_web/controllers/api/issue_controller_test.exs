defmodule ArbiterWeb.Api.IssueControllerTest do
  use ArbiterWeb.ConnCase, async: false

  alias Arbiter.Tasks.{Dependency, Issue, Workspace}

  setup %{conn: conn} do
    {:ok, ws} = Ash.create(Workspace, %{name: "api-test-ws", prefix: "api"})
    {:ok, conn: put_req_header(conn, "accept", "application/json"), ws: ws}
  end

  describe "POST /api/issues" do
    # #1973: `arb create --parent` sends the parent along, so a child of a
    # tracker-linked parent stays local with the parent's ticket as context
    # instead of minting its own upstream ticket.
    test "parent_id naming a tracker-linked parent defaults the child to context-only", %{
      conn: conn
    } do
      Req.Test.stub(Arbiter.Trackers.Jira.HTTP, fn _ -> flunk("must not call Jira") end)

      {:ok, jira_ws} =
        Ash.create(Workspace, %{
          name: "api-jira-ws",
          prefix: "apj",
          config: %{"tracker" => %{"type" => "jira"}}
        })

      {:ok, parent} =
        Ash.create(Issue, %{
          title: "tracked story",
          workspace_id: jira_ws.id,
          tracker_type: :jira,
          tracker_ref: "VR-19083"
        })

      conn =
        post(conn, ~p"/api/issues", %{
          title: "a slice",
          workspace_id: jira_ws.id,
          parent_id: parent.id
        })

      assert %{"id" => id, "tracker_type" => "none"} = json_response(conn, 201)

      child = Ash.get!(Issue, id)
      assert child.tracker_ref == nil
      assert child.tracker_context_type == :jira
      assert child.tracker_context_ref == "VR-19083"
    end

    test "creates issue with valid attrs", %{conn: conn, ws: ws} do
      conn =
        post(conn, ~p"/api/issues", %{
          title: "first",
          workspace_id: ws.id,
          priority: 1,
          issue_type: "bug"
        })

      assert %{
               "id" => id,
               "title" => "first",
               "status" => "open",
               "priority" => 1,
               "issue_type" => "bug",
               "workspace_id" => ws_id
             } = json_response(conn, 201)

      assert String.starts_with?(id, "api-")
      assert ws_id == ws.id
    end

    # bd-7mbrlg
    test "warns (non-blocking) when a gated type is created with no acceptance criteria", %{
      conn: conn,
      ws: ws
    } do
      conn =
        post(conn, ~p"/api/issues", %{title: "no ACs", workspace_id: ws.id, issue_type: "bug"})

      body = json_response(conn, 201)
      assert [warning] = body["warnings"]
      assert warning =~ "acceptance criteria"
    end

    test "no warning when acceptance criteria are given", %{conn: conn, ws: ws} do
      conn =
        post(conn, ~p"/api/issues", %{
          title: "has ACs",
          workspace_id: ws.id,
          issue_type: "bug",
          acceptance: "- it works"
        })

      body = json_response(conn, 201)
      refute Map.has_key?(body, "warnings")
    end

    test "no warning for exempt types (task/decision/epic) with no acceptance criteria", %{
      conn: conn,
      ws: ws
    } do
      conn =
        post(conn, ~p"/api/issues", %{title: "a task", workspace_id: ws.id, issue_type: "task"})

      body = json_response(conn, 201)
      refute Map.has_key?(body, "warnings")
    end

    # bd-1ozks5: the local assignee field is gone, but an existing coordinator
    # script/prompt may still pass it — accept and ignore, with a warning,
    # rather than failing the create.
    test "accepts and ignores a deprecated `assignee` param, with a warning", %{
      conn: conn,
      ws: ws
    } do
      conn =
        post(conn, ~p"/api/issues", %{
          title: "still has assignee",
          workspace_id: ws.id,
          issue_type: "task",
          assignee: "alice"
        })

      body = json_response(conn, 201)
      refute Map.has_key?(body, "assignee")
      assert [warning] = body["warnings"]
      assert warning =~ "assignee"
    end

    test "accepts and persists `difficulty` (0..5)", %{conn: conn, ws: ws} do
      conn =
        post(conn, ~p"/api/issues", %{
          title: "d3-feature",
          workspace_id: ws.id,
          difficulty: 3
        })

      assert %{"id" => id, "difficulty" => 3} = json_response(conn, 201)

      conn = get(conn, ~p"/api/issues/#{id}")
      assert %{"difficulty" => 3} = json_response(conn, 200)
    end

    test "accepts `difficulty: 5` at the new ceiling (#1519)", %{conn: conn, ws: ws} do
      conn =
        post(conn, ~p"/api/issues", %{
          title: "d5-flagship",
          workspace_id: ws.id,
          difficulty: 5
        })

      assert %{"id" => id, "difficulty" => 5} = json_response(conn, 201)

      conn = get(conn, ~p"/api/issues/#{id}")
      assert %{"difficulty" => 5} = json_response(conn, 200)
    end

    test "accepts, persists, and renders `repo` (bd-2jum8j)", %{conn: conn, ws: ws} do
      conn =
        post(conn, ~p"/api/issues", %{title: "assigned", workspace_id: ws.id, repo: "org/tonic"})

      assert %{"id" => id, "repo" => "org/tonic"} = json_response(conn, 201)
      assert Ash.get!(Issue, id).repo == "org/tonic"

      conn = patch(conn, ~p"/api/issues/#{id}", %{repo: "org/tonic_device"})
      assert %{"repo" => "org/tonic_device"} = json_response(conn, 200)
      assert Ash.get!(Issue, id).repo == "org/tonic_device"
    end

    test "leaves difficulty nil when omitted", %{conn: conn, ws: ws} do
      conn =
        post(conn, ~p"/api/issues", %{
          title: "no-difficulty",
          workspace_id: ws.id
        })

      assert %{"difficulty" => nil} = json_response(conn, 201)
    end

    test "returns 422 with validation_error on missing title", %{conn: conn, ws: ws} do
      conn = post(conn, ~p"/api/issues", %{workspace_id: ws.id})

      assert %{"error" => %{"type" => "validation_error"}} = json_response(conn, 422)
    end

    test "returns 502 when upstream-create fails (task body + structured error)",
         %{conn: conn} do
      env_var = "GTE_CONTROLLER_OUTBOUND_TEST_TOKEN"
      System.put_env(env_var, "tok")
      on_exit(fn -> System.delete_env(env_var) end)

      {:ok, gh_ws} =
        Ash.create(Workspace, %{
          name: "ctrl-gh",
          prefix: "gh",
          config: %{
            "tracker" => %{
              "type" => "github",
              "config" => %{
                "owner" => "o",
                "repo" => "r",
                "credentials_ref" => "env:#{env_var}"
              }
            }
          }
        })

      Req.Test.stub(Arbiter.Trackers.GitHub.HTTP, fn conn ->
        conn
        |> Plug.Conn.put_status(500)
        |> Req.Test.json(%{"message" => "upstream down"})
      end)

      conn = post(conn, ~p"/api/issues", %{title: "half", workspace_id: gh_ws.id})

      body = json_response(conn, 502)

      assert %{
               "issue" => %{"id" => task_id, "title" => "half"},
               "error" => %{
                 "type" => "upstream_create_failed",
                 "message" => msg,
                 "details" => %{"task_id" => task_id, "tracker_type" => "github"}
               }
             } = body

      assert is_binary(task_id)
      assert msg =~ "upstream github create failed"
    end

    test "returns 409 when an open task with the same title exists", %{conn: conn, ws: ws} do
      {:ok, _existing} = Ash.create(Issue, %{title: "Duplicate Title", workspace_id: ws.id})

      conn = post(conn, ~p"/api/issues", %{title: "Duplicate Title", workspace_id: ws.id})

      body = json_response(conn, 409)

      assert %{
               "error" => %{
                 "type" => "duplicate_task",
                 "message" => msg,
                 "details" => %{"matches" => [%{"title" => "Duplicate Title"}]}
               }
             } = body

      assert msg =~ "--force"
    end

    test "dedup is case-insensitive and trims whitespace", %{conn: conn, ws: ws} do
      {:ok, _existing} = Ash.create(Issue, %{title: "Foo Bar", workspace_id: ws.id})

      conn = post(conn, ~p"/api/issues", %{title: "  foo bar  ", workspace_id: ws.id})

      assert %{"error" => %{"type" => "duplicate_task"}} = json_response(conn, 409)
    end

    test "no dedup for tasks in a different workspace", %{conn: conn, ws: ws} do
      {:ok, ws2} = Ash.create(Workspace, %{name: "other-dedup", prefix: "oth2"})
      {:ok, _existing} = Ash.create(Issue, %{title: "Unique Title", workspace_id: ws2.id})

      conn = post(conn, ~p"/api/issues", %{title: "Unique Title", workspace_id: ws.id})

      assert json_response(conn, 201)
    end

    test "no dedup when the matching task is closed", %{conn: conn, ws: ws} do
      {:ok, existing} = Ash.create(Issue, %{title: "Closed Task", workspace_id: ws.id})
      {:ok, _} = Ash.update(existing, %{}, action: :close)

      conn = post(conn, ~p"/api/issues", %{title: "Closed Task", workspace_id: ws.id})

      assert json_response(conn, 201)
    end

    test "--force bypasses the local dedup check", %{conn: conn, ws: ws} do
      {:ok, _existing} = Ash.create(Issue, %{title: "Forced Title", workspace_id: ws.id})

      conn =
        post(conn, ~p"/api/issues", %{title: "Forced Title", workspace_id: ws.id, force: true})

      assert json_response(conn, 201)
    end

    test "returns 409 when GitHub has an open issue with the same title", %{conn: conn} do
      env_var = "GTE_TRACKER_DEDUP_TEST_TOKEN"
      System.put_env(env_var, "tok")
      on_exit(fn -> System.delete_env(env_var) end)

      {:ok, gh_ws} =
        Ash.create(Workspace, %{
          name: "dedup-gh",
          prefix: "ddp",
          config: %{
            "tracker" => %{
              "type" => "github",
              "config" => %{
                "owner" => "o",
                "repo" => "r",
                "credentials_ref" => "env:#{env_var}"
              }
            }
          }
        })

      Req.Test.stub(Arbiter.Trackers.GitHub.HTTP, fn conn ->
        cond do
          conn.request_path == "/search/issues" ->
            conn
            |> Plug.Conn.put_status(200)
            |> Req.Test.json(%{
              "total_count" => 1,
              "items" => [
                %{
                  "number" => 99,
                  "title" => "tracker dup",
                  "html_url" => "https://github.com/o/r/issues/99",
                  "state" => "open"
                }
              ]
            })

          true ->
            conn
            |> Plug.Conn.put_status(201)
            |> Req.Test.json(%{"number" => 100})
        end
      end)

      conn = post(conn, ~p"/api/issues", %{title: "tracker dup", workspace_id: gh_ws.id})

      body = json_response(conn, 409)

      assert %{
               "error" => %{
                 "type" => "duplicate_tracker_issue",
                 "message" => msg,
                 "details" => %{"matches" => [%{"ref" => "99", "url" => url}]}
               }
             } = body

      assert msg =~ "--force"
      assert url =~ "issues/99"
    end

    test "--force bypasses the tracker dedup check", %{conn: conn} do
      env_var = "GTE_TRACKER_DEDUP_FORCE_TEST_TOKEN"
      System.put_env(env_var, "tok")
      on_exit(fn -> System.delete_env(env_var) end)

      {:ok, gh_ws} =
        Ash.create(Workspace, %{
          name: "dedup-gh-force",
          prefix: "ddf",
          config: %{
            "tracker" => %{
              "type" => "github",
              "config" => %{
                "owner" => "o",
                "repo" => "r",
                "credentials_ref" => "env:#{env_var}"
              }
            }
          }
        })

      Req.Test.stub(Arbiter.Trackers.GitHub.HTTP, fn conn ->
        cond do
          conn.request_path == "/search/issues" ->
            Req.Test.json(conn, %{
              "total_count" => 1,
              "items" => [
                %{
                  "number" => 99,
                  "title" => "forced dup",
                  "html_url" => "https://github.com/o/r/issues/99",
                  "state" => "open"
                }
              ]
            })

          true ->
            conn
            |> Plug.Conn.put_status(201)
            |> Req.Test.json(%{"number" => 101})
        end
      end)

      conn =
        post(conn, ~p"/api/issues", %{
          title: "forced dup",
          workspace_id: gh_ws.id,
          force: true
        })

      assert json_response(conn, 201)
    end

    test "tracker search error is silently ignored (does not block create)", %{conn: conn} do
      env_var = "GTE_TRACKER_DEDUP_ERR_TEST_TOKEN"
      System.put_env(env_var, "tok")
      on_exit(fn -> System.delete_env(env_var) end)

      {:ok, gh_ws} =
        Ash.create(Workspace, %{
          name: "dedup-gh-err",
          prefix: "dde",
          config: %{
            "tracker" => %{
              "type" => "github",
              "config" => %{
                "owner" => "o",
                "repo" => "r",
                "credentials_ref" => "env:#{env_var}"
              }
            }
          }
        })

      Req.Test.stub(Arbiter.Trackers.GitHub.HTTP, fn conn ->
        cond do
          conn.request_path == "/search/issues" ->
            conn
            |> Plug.Conn.put_status(500)
            |> Req.Test.json(%{"message" => "search down"})

          true ->
            conn
            |> Plug.Conn.put_status(201)
            |> Req.Test.json(%{"number" => 102})
        end
      end)

      conn = post(conn, ~p"/api/issues", %{title: "search error task", workspace_id: gh_ws.id})

      assert json_response(conn, 201)
    end
  end

  # bd-9dwbvt: `arb issue create` / `arb create` post here, so this is the CLI's
  # slice of "every issue carries a repo".
  describe "POST /api/issues — repo resolution (bd-9dwbvt)" do
    defp repo_ws!(config) do
      {:ok, ws} =
        Ash.create(Workspace, %{
          name: "api-repo-#{System.unique_integer([:positive])}",
          prefix: "apr",
          config: config
        })

      ws
    end

    test "auto-fills the workspace's only repo", %{conn: conn} do
      ws = repo_ws!(%{"repo_paths" => %{"tonic" => "/srv/tonic"}})

      conn = post(conn, ~p"/api/issues", %{title: "sole", workspace_id: ws.id})

      assert %{"id" => id, "repo" => "tonic"} = json_response(conn, 201)
      assert Ash.get!(Issue, id).repo == "tonic"
    end

    test "falls back to the workspace default_repo", %{conn: conn} do
      ws =
        repo_ws!(%{
          "repo_paths" => %{"tonic" => "/srv/tonic", "tonic_device" => "/srv/device"},
          "default_repo" => "tonic_device"
        })

      conn = post(conn, ~p"/api/issues", %{title: "defaulted", workspace_id: ws.id})

      assert %{"repo" => "tonic_device"} = json_response(conn, 201)
    end

    test "422s with the configured keys when nothing resolves", %{conn: conn} do
      ws =
        repo_ws!(%{"repo_paths" => %{"tonic" => "/srv/tonic", "tonic_device" => "/srv/device"}})

      conn = post(conn, ~p"/api/issues", %{title: "ambiguous", workspace_id: ws.id})

      assert %{"error" => %{"type" => "validation_error"} = error} = json_response(conn, 422)
      assert inspect(error) =~ "tonic_device"
    end

    test "422s on a repo that is not a configured repo_paths key", %{conn: conn} do
      ws = repo_ws!(%{"repo_paths" => %{"tonic" => "/srv/tonic"}})

      conn = post(conn, ~p"/api/issues", %{title: "typo", workspace_id: ws.id, repo: "tonc"})

      assert %{"error" => %{"type" => "validation_error"} = error} = json_response(conn, 422)
      assert inspect(error) =~ "tonc"
    end
  end

  describe "GET /api/issues/:id" do
    test "returns the issue as a bare object", %{conn: conn, ws: ws} do
      {:ok, issue} = Ash.create(Issue, %{title: "show me", workspace_id: ws.id})

      conn = get(conn, ~p"/api/issues/#{issue.id}")

      body = json_response(conn, 200)
      assert body["id"] == issue.id
      assert body["title"] == "show me"
      assert body["status"] == "open"
    end

    # bd-842qio: the stored lifecycle state beside the legacy status — what
    # `arb issue show --json` prints.
    test "carries the lifecycle state, close_reason and rank", %{conn: conn, ws: ws} do
      {:ok, issue} = Ash.create(Issue, %{title: "show my state", workspace_id: ws.id})

      body = conn |> get(~p"/api/issues/#{issue.id}") |> json_response(200)
      assert body["state"] == "backlog"
      assert Map.has_key?(body, "close_reason") and body["close_reason"] == nil
      assert body["rank"] == issue.rank

      {:ok, _} = Ash.update(issue, %{close_reason: :duplicate}, action: :close)

      body = conn |> get(~p"/api/issues/#{issue.id}") |> json_response(200)

      assert {body["state"], body["status"], body["close_reason"]} ==
               {"closed", "closed", "duplicate"}
    end

    test "returns 404 for missing issue", %{conn: conn} do
      conn = get(conn, ~p"/api/issues/api-doesnotexist")
      assert %{"error" => %{"type" => "not_found"}} = json_response(conn, 404)
    end

    # bd-1defgu: `arb issue show` was write-only for dependency edges — you
    # could `arb dep add` one onto this task and never see it again short of
    # opening the DB.
    test "includes the issue's dependency edges", %{conn: conn, ws: ws} do
      {:ok, issue} = Ash.create(Issue, %{title: "show me", workspace_id: ws.id})
      {:ok, other} = Ash.create(Issue, %{title: "the other one", workspace_id: ws.id})

      {:ok, dep} =
        Arbiter.Tasks.Dependencies.add(issue.id, other.id, :conflicts_with)

      body = conn |> get(~p"/api/issues/#{issue.id}") |> json_response(200)

      assert [row] = body["dependencies"]
      assert row["id"] == dep.id
      assert row["type"] == "conflicts_with"
      assert row["to"]["id"] == other.id
      assert row["to"]["title"] == "the other one"
    end

    test "dependencies is an empty list when the issue has no edges", %{conn: conn, ws: ws} do
      {:ok, issue} = Ash.create(Issue, %{title: "lonely", workspace_id: ws.id})

      body = conn |> get(~p"/api/issues/#{issue.id}") |> json_response(200)

      assert body["dependencies"] == []
    end

    # bd-3j4ch4 AC5: `arb issue show` renders the cost estimate, and this is
    # where it gets the numbers from.
    test "carries the cost estimate when the ledger has enough history", %{conn: conn, ws: ws} do
      for cost <- Enum.map(1..10, &(&1 * 1.0)) do
        {:ok, past} =
          Ash.create(Issue, %{
            title: "history",
            workspace_id: ws.id,
            difficulty: 2,
            issue_type: :feature
          })

        {:ok, closed} = Ash.update(past, %{close_upstream: false}, action: :close)

        {:ok, _ev} =
          Ash.create(Arbiter.Usage.Event, %{
            task_id: closed.id,
            base_task_id: closed.id,
            role: "base",
            source: :task,
            step: :work,
            workspace_id: ws.id,
            cost_usd: cost,
            occurred_at: DateTime.utc_now()
          })
      end

      {:ok, issue} =
        Ash.create(Issue, %{
          title: "size me",
          workspace_id: ws.id,
          difficulty: 2,
          issue_type: :feature
        })

      body = conn |> get(~p"/api/issues/#{issue.id}") |> json_response(200)

      assert body["estimate"]["range"] == [3.0, 8.0]
      assert body["estimate"]["median"] == 5.0
      assert body["estimate"]["p90"] == 9.0
      assert body["estimate"]["n"] == 10
      assert body["estimate"]["basis"] == "difficulty+type"
      assert body["estimate"]["fallback_level"] == 0
    end

    test "estimate is null when the ledger is empty", %{conn: conn, ws: ws} do
      {:ok, issue} = Ash.create(Issue, %{title: "no history", workspace_id: ws.id})

      body = conn |> get(~p"/api/issues/#{issue.id}") |> json_response(200)

      assert Map.has_key?(body, "estimate")
      assert body["estimate"] == nil
    end

    # bd-18vl9q AC3: `arb issue show` renders the epic cost rollup for an
    # `:epic` issue.
    test "carries the epic cost rollup for an epic", %{conn: conn, ws: ws} do
      {:ok, epic} = Ash.create(Issue, %{title: "an epic", workspace_id: ws.id, issue_type: :epic})

      {:ok, child} =
        Ash.create(Issue, %{title: "a child", workspace_id: ws.id, issue_type: :task})

      {:ok, closed} = Ash.update(child, %{close_upstream: false}, action: :close)

      {:ok, _ev} =
        Ash.create(Arbiter.Usage.Event, %{
          task_id: closed.id,
          base_task_id: closed.id,
          role: "base",
          source: :task,
          step: :work,
          workspace_id: ws.id,
          cost_usd: 4.25,
          occurred_at: DateTime.utc_now()
        })

      {:ok, _} = Arbiter.Tasks.Dependencies.add(epic.id, closed.id, :parent_of)

      body = conn |> get(~p"/api/issues/#{epic.id}") |> json_response(200)

      assert body["epic_rollup"]["spent"] == 4.25
      assert body["epic_rollup"]["closed_count"] == 1
    end

    test "epic_rollup is null for a non-epic issue", %{conn: conn, ws: ws} do
      {:ok, issue} = Ash.create(Issue, %{title: "not an epic", workspace_id: ws.id})

      body = conn |> get(~p"/api/issues/#{issue.id}") |> json_response(200)

      assert Map.has_key?(body, "epic_rollup")
      assert body["epic_rollup"] == nil
    end
  end

  describe "GET /api/issues" do
    test "lists all issues wrapped in data", %{conn: conn, ws: ws} do
      {:ok, _i1} = Ash.create(Issue, %{title: "a", workspace_id: ws.id})
      {:ok, _i2} = Ash.create(Issue, %{title: "b", workspace_id: ws.id})

      conn = get(conn, ~p"/api/issues")
      assert %{"data" => list} = json_response(conn, 200)
      assert length(list) == 2
    end

    test "filters by status", %{conn: conn, ws: ws} do
      {:ok, open_issue} = Ash.create(Issue, %{title: "still open", workspace_id: ws.id})
      {:ok, will_close} = Ash.create(Issue, %{title: "to close", workspace_id: ws.id})
      {:ok, _closed} = Ash.update(will_close, %{}, action: :close)

      conn = get(conn, ~p"/api/issues?status=open")
      assert %{"data" => list} = json_response(conn, 200)
      assert Enum.any?(list, &(&1["id"] == open_issue.id))
      refute Enum.any?(list, &(&1["id"] == will_close.id))
    end

    test "filters by workspace_id", %{conn: conn, ws: ws} do
      {:ok, ws2} = Ash.create(Workspace, %{name: "other", prefix: "oth"})
      {:ok, mine} = Ash.create(Issue, %{title: "mine", workspace_id: ws.id})
      {:ok, _other} = Ash.create(Issue, %{title: "other", workspace_id: ws2.id})

      conn = get(conn, ~p"/api/issues?workspace_id=#{ws.id}")
      assert %{"data" => list} = json_response(conn, 200)
      assert Enum.all?(list, &(&1["workspace_id"] == ws.id))
      assert Enum.any?(list, &(&1["id"] == mine.id))
    end

    test "returns 400 for unknown status value", %{conn: conn} do
      conn = get(conn, ~p"/api/issues?status=zzzzz_not_an_atom_zzzzz")
      assert %{"error" => %{"type" => "invalid_request"}} = json_response(conn, 400)
    end
  end

  describe "PATCH /api/issues/:id" do
    test "updates allowed fields", %{conn: conn, ws: ws} do
      {:ok, issue} = Ash.create(Issue, %{title: "before", workspace_id: ws.id})

      conn = patch(conn, ~p"/api/issues/#{issue.id}", %{title: "after", priority: 0})

      body = json_response(conn, 200)
      assert body["title"] == "after"
      assert body["priority"] == 0
    end

    test "ignores workspace_id (immutable post-create)", %{conn: conn, ws: ws} do
      {:ok, ws2} = Ash.create(Workspace, %{name: "other", prefix: "oth"})
      {:ok, issue} = Ash.create(Issue, %{title: "stay-put", workspace_id: ws.id})

      conn = patch(conn, ~p"/api/issues/#{issue.id}", %{title: "renamed", workspace_id: ws2.id})

      body = json_response(conn, 200)
      assert body["workspace_id"] == ws.id
    end

    test "returns 404 on missing", %{conn: conn} do
      conn = patch(conn, ~p"/api/issues/api-nope", %{title: "x"})
      assert %{"error" => %{"type" => "not_found"}} = json_response(conn, 404)
    end

    test "persists and serializes pr_body (bd-53xrmi)", %{conn: conn, ws: ws} do
      {:ok, issue} = Ash.create(Issue, %{title: "needs a body", workspace_id: ws.id})

      conn =
        patch(conn, ~p"/api/issues/#{issue.id}", %{
          pr_body: "## Summary\nWorker-authored writeup."
        })

      body = json_response(conn, 200)
      assert body["pr_body"] == "## Summary\nWorker-authored writeup."

      assert Ash.get!(Issue, issue.id).pr_body == "## Summary\nWorker-authored writeup."
    end

    # bd-1ozks5: accept and ignore a deprecated `assignee` on update too.
    test "accepts and ignores a deprecated `assignee` param, with a warning", %{
      conn: conn,
      ws: ws
    } do
      {:ok, issue} = Ash.create(Issue, %{title: "before", workspace_id: ws.id})

      conn = patch(conn, ~p"/api/issues/#{issue.id}", %{title: "after", assignee: "bob"})

      body = json_response(conn, 200)
      assert body["title"] == "after"
      refute Map.has_key?(body, "assignee")
      assert [warning] = body["warnings"]
      assert warning =~ "assignee"
    end
  end

  describe "POST /api/issues/:id/close" do
    test "closes an open issue", %{conn: conn, ws: ws} do
      {:ok, issue} = Ash.create(Issue, %{title: "close me", workspace_id: ws.id})

      conn = post(conn, ~p"/api/issues/#{issue.id}/close", %{reason: "done"})

      body = json_response(conn, 200)
      assert body["status"] == "closed"
      refute is_nil(body["closed_at"])
    end

    test "returns 422 closing an already-closed issue", %{conn: conn, ws: ws} do
      {:ok, issue} = Ash.create(Issue, %{title: "x", workspace_id: ws.id})
      {:ok, closed} = Ash.update(issue, %{}, action: :close)

      conn = post(conn, ~p"/api/issues/#{closed.id}/close")
      assert %{"error" => %{"type" => "validation_error"}} = json_response(conn, 422)
    end
  end

  describe "POST /api/issues/:id/reopen" do
    test "reopens a closed issue", %{conn: conn, ws: ws} do
      {:ok, issue} = Ash.create(Issue, %{title: "reopen me", workspace_id: ws.id})
      {:ok, closed} = Ash.update(issue, %{}, action: :close)

      conn = post(conn, ~p"/api/issues/#{closed.id}/reopen")

      body = json_response(conn, 200)
      assert body["status"] == "open"
      assert is_nil(body["closed_at"])
    end

    test "returns 422 reopening an already-open issue", %{conn: conn, ws: ws} do
      {:ok, issue} = Ash.create(Issue, %{title: "x", workspace_id: ws.id})

      conn = post(conn, ~p"/api/issues/#{issue.id}/reopen")
      assert %{"error" => %{"type" => "validation_error"}} = json_response(conn, 422)
    end
  end

  describe "POST /api/issues/:id/promote" do
    test "promotes a task from Backlog to Ready", %{conn: conn, ws: ws} do
      {:ok, issue} =
        Ash.create(Issue, %{title: "promote me", workspace_id: ws.id, acceptance: "- works"})

      assert issue.refined == false

      conn = post(conn, ~p"/api/issues/#{issue.id}/promote")

      body = json_response(conn, 200)
      assert body["refined"] == true
    end

    test "promoting an already-refined task is a no-op success", %{conn: conn, ws: ws} do
      {:ok, issue} =
        Ash.create(Issue, %{title: "x", workspace_id: ws.id, acceptance: "- works"})

      {:ok, refined} = Ash.update(issue, %{}, action: :promote_to_ready)
      assert refined.refined == true

      conn = post(conn, ~p"/api/issues/#{refined.id}/promote")

      body = json_response(conn, 200)
      assert body["refined"] == true
    end

    # bd-7mbrlg
    test "refuses a bug/feature/chore with no acceptance criteria and no waiver", %{
      conn: conn,
      ws: ws
    } do
      {:ok, issue} =
        Ash.create(Issue, %{title: "no ACs", workspace_id: ws.id, issue_type: :bug})

      conn = post(conn, ~p"/api/issues/#{issue.id}/promote")

      body = json_response(conn, 422)
      assert body["error"]["type"] == "validation_error"
      assert body["error"]["message"] =~ "acceptance criteria"
    end

    test "an acceptance_waived reason allows promotion and is persisted", %{conn: conn, ws: ws} do
      {:ok, issue} =
        Ash.create(Issue, %{title: "waived", workspace_id: ws.id, issue_type: :chore})

      conn =
        post(conn, ~p"/api/issues/#{issue.id}/promote", %{
          "acceptance_waived" => "trivial config bump"
        })

      body = json_response(conn, 200)
      assert body["refined"] == true
      assert body["acceptance_waived"] == "trivial config bump"
    end

    test "task/decision/epic promote fine with no acceptance criteria (exempt)", %{
      conn: conn,
      ws: ws
    } do
      for type <- [:task, :decision, :epic] do
        {:ok, issue} =
          Ash.create(Issue, %{title: "exempt #{type}", workspace_id: ws.id, issue_type: type})

        conn = post(conn, ~p"/api/issues/#{issue.id}/promote")
        assert json_response(conn, 200)["refined"] == true
      end
    end
  end

  describe "POST /api/issues/:id/demote" do
    test "demotes a task from Ready to Backlog", %{conn: conn, ws: ws} do
      {:ok, issue} =
        Ash.create(Issue, %{title: "demote me", workspace_id: ws.id, acceptance: "- works"})

      {:ok, refined} = Ash.update(issue, %{}, action: :promote_to_ready)
      assert refined.refined == true

      conn = post(conn, ~p"/api/issues/#{refined.id}/demote")

      body = json_response(conn, 200)
      assert body["refined"] == false
    end

    test "demoting an already-backlog task is a no-op success", %{conn: conn, ws: ws} do
      {:ok, issue} =
        Ash.create(Issue, %{title: "x", workspace_id: ws.id})

      assert issue.refined == false

      conn = post(conn, ~p"/api/issues/#{issue.id}/demote")

      body = json_response(conn, 200)
      assert body["refined"] == false
    end
  end

  # bd-9so315 — post-merge verification over REST (the `arb` CLI's transport).
  describe "verify_after_deploy over REST" do
    test "create + patch set the flag and it is rendered", %{conn: conn, ws: ws} do
      conn1 =
        post(conn, ~p"/api/issues", %{
          title: "flagged",
          workspace_id: ws.id,
          verify_after_deploy: true
        })

      body = json_response(conn1, 201)
      assert body["verify_after_deploy"] == true

      conn2 = patch(conn, ~p"/api/issues/#{body["id"]}", %{verify_after_deploy: false})
      assert json_response(conn2, 200)["verify_after_deploy"] == false
    end
  end

  describe "POST /api/issues/:id/verify" do
    setup %{ws: ws} do
      {:ok, issue} =
        Ash.create(Issue, %{title: "park me", workspace_id: ws.id, verify_after_deploy: true})

      # bd-842qio: only work in progress parks for verification.
      {:ok, issue} = Ash.update(issue, %{status: :in_progress})
      {:ok, awaiting} = Ash.update(issue, %{}, action: :await_verification)
      {:ok, awaiting: awaiting}
    end

    test "observed closes the task and persists the evidence", %{conn: conn, awaiting: task} do
      conn =
        post(conn, ~p"/api/issues/#{task.id}/verify", %{
          outcome: "observed",
          evidence: "restarted; new capture_source path fires"
        })

      body = json_response(conn, 200)
      assert body["status"] == "closed"
      assert body["verification_outcome"] == "observed"
      assert body["verification_evidence"] == "restarted; new capture_source path fires"
    end

    test "failed reopens the task and persists the evidence", %{conn: conn, awaiting: task} do
      conn =
        post(conn, ~p"/api/issues/#{task.id}/verify", %{
          outcome: "failed",
          evidence: "doctor still green with zero repos"
        })

      body = json_response(conn, 200)
      assert body["status"] == "open"
      assert body["verification_outcome"] == "failed"
      assert body["verification_evidence"] == "doctor still green with zero repos"
    end

    test "a task that is not awaiting verification is rejected", %{conn: conn, ws: ws} do
      {:ok, other} = Ash.create(Issue, %{title: "not parked", workspace_id: ws.id})

      conn = post(conn, ~p"/api/issues/#{other.id}/verify", %{outcome: "observed", evidence: "x"})

      assert json_response(conn, 422)
    end

    test "blank evidence is rejected", %{conn: conn, awaiting: task} do
      conn = post(conn, ~p"/api/issues/#{task.id}/verify", %{outcome: "observed", evidence: "  "})
      assert json_response(conn, 422)
    end
  end

  describe "GET /api/issues/ready" do
    test "returns only queued issues with no open blockers", %{conn: conn, ws: ws} do
      # bd-6zapbl: the Ready column — queued tickets, never Backlog ones.
      queued = fn title ->
        {:ok, issue} = Ash.create(Issue, %{title: title, workspace_id: ws.id})

        {:ok, issue} =
          Ash.update(issue, %{acceptance_waived: "fixture"}, action: :promote_to_ready)

        issue
      end

      blocker = queued.("blocker")
      blocked = queued.("blocked")
      free = queued.("free")
      {:ok, backlog} = Ash.create(Issue, %{title: "backlog", workspace_id: ws.id})

      {:ok, _} =
        Ash.create(Dependency, %{
          from_issue_id: blocked.id,
          to_issue_id: blocker.id,
          type: :depends_on
        })

      conn = get(conn, ~p"/api/issues/ready")
      assert %{"data" => list} = json_response(conn, 200)
      ids = Enum.map(list, & &1["id"])

      # blocker has no incoming gating deps from itself, it's ready;
      # blocked is gated by an open issue, NOT ready.
      assert blocker.id in ids
      assert free.id in ids
      refute blocked.id in ids
      refute backlog.id in ids
    end
  end

  # bd-9zuvbh — the ReviewGate park has to be visible to a human, or class C's
  # terminal state is just a quieter way of losing the work.
  describe "GET /api/issues/review_parked" do
    test "returns parked tasks with their reason, oldest first", %{conn: conn, ws: ws} do
      {:ok, plain} = Ash.create(Issue, %{title: "not parked", workspace_id: ws.id})

      {:ok, older} = Ash.create(Issue, %{title: "older park", workspace_id: ws.id})
      {:ok, newer} = Ash.create(Issue, %{title: "newer park", workspace_id: ws.id})

      {:ok, :claimed, _} = Arbiter.Tasks.ReviewPark.park(older.id, :inconclusive)
      {:ok, :claimed, _} = Arbiter.Tasks.ReviewPark.park(newer.id, :reviewer_timeout)

      conn = get(conn, ~p"/api/issues/review_parked")
      assert %{"data" => list} = json_response(conn, 200)

      ids = Enum.map(list, & &1["id"])
      assert [older.id, newer.id] == Enum.filter(ids, &(&1 in [older.id, newer.id]))
      refute plain.id in ids

      assert Enum.find(list, &(&1["id"] == older.id))["review_park_reason"] == "inconclusive"
      assert Enum.find(list, &(&1["id"] == newer.id))["review_park_reason"] == "reviewer_timeout"
      assert Enum.find(list, &(&1["id"] == newer.id))["review_parked_at"]
    end

    test "an unparked issue reports the field as null", %{conn: conn, ws: ws} do
      {:ok, plain} = Ash.create(Issue, %{title: "plain", workspace_id: ws.id})

      conn = get(conn, ~p"/api/issues/#{plain.id}")
      assert %{"review_park_reason" => nil, "review_parked_at" => nil} = json_response(conn, 200)
    end
  end
end
