defmodule ArbiterWeb.CoreComponents.DataTest do
  use ExUnit.Case, async: true

  use Phoenix.Component
  import Phoenix.LiveViewTest
  import ArbiterWeb.CoreComponents.Data

  describe "status_chip/1" do
    test "renders the literal status value, never prettified" do
      html = render_component(&status_chip/1, status: :handed_off)

      assert html =~ "handed_off"
      refute html =~ "Handed off"
      refute html =~ "Handed Off"
    end

    test "renders literal in_progress verbatim" do
      html = render_component(&status_chip/1, status: :in_progress)

      assert html =~ "in_progress"
      refute html =~ "In Progress"
    end

    test "accepts string statuses verbatim too" do
      html = render_component(&status_chip/1, status: "weird_custom_status")

      assert html =~ "weird_custom_status"
    end

    test "known statuses get a semantic badge class" do
      html = render_component(&status_chip/1, status: :succeeded)
      assert html =~ "badge-success"

      html = render_component(&status_chip/1, status: :failed)
      assert html =~ "badge-error"
    end

    test "run states and outcomes get a semantic badge class" do
      # bd-1uu19b: a run chip shows its state while live, its outcome once
      # finished (`StatusHelpers.run_status/1`).
      assert render_component(&status_chip/1, status: :starting) =~ "badge-info"
      assert render_component(&status_chip/1, status: :working) =~ "badge-info"
      assert render_component(&status_chip/1, status: :waiting) =~ "badge-warning"
      assert render_component(&status_chip/1, status: :interrupted) =~ "badge-warning"
      assert render_component(&status_chip/1, status: :handed_off) =~ "badge-ghost"
      assert render_component(&status_chip/1, status: "waiting") =~ "badge-warning"
    end

    test "unknown status falls back to a ghost badge without crashing" do
      html = render_component(&status_chip/1, status: :some_unmapped_status)

      assert html =~ "badge-ghost"
      assert html =~ "some_unmapped_status"
    end

    test "Issue status vocabulary gets a semantic badge class" do
      html = render_component(&status_chip/1, status: :open)
      assert html =~ "badge-success"

      html = render_component(&status_chip/1, status: :in_progress)
      assert html =~ "badge-info"

      html = render_component(&status_chip/1, status: :closed)
      assert html =~ "badge-ghost"
    end
  end

  describe "priority_tag/1" do
    test "P0 and P1 tint red" do
      assert render_component(&priority_tag/1, priority: 0) =~ "badge-error"
      assert render_component(&priority_tag/1, priority: 1) =~ "badge-error"
    end

    test "P2 is neutral" do
      html = render_component(&priority_tag/1, priority: 2)
      assert html =~ "badge-neutral"
    end

    test "P3 and P4 are ghost" do
      assert render_component(&priority_tag/1, priority: 3) =~ "badge-ghost"
      assert render_component(&priority_tag/1, priority: 4) =~ "badge-ghost"
    end

    test "renders the P-prefixed label" do
      html = render_component(&priority_tag/1, priority: 1)
      assert html =~ "P1"
    end

    test "nil priority renders a placeholder ghost tag" do
      html = render_component(&priority_tag/1, priority: nil)
      assert html =~ "badge-ghost"
      assert html =~ "—"
    end
  end

  describe "difficulty_meter/1" do
    test "D0 fills zero of five bars with a dotted border to distinguish from nil" do
      html = render_component(&difficulty_meter/1, difficulty: 0)

      assert count_occurrences(html, "difficulty-bar-filled") == 0
      assert count_occurrences(html, "difficulty-bar-empty") == 5
      assert html =~ "border-dashed"
    end

    test "D2 fills exactly two of five bars" do
      html = render_component(&difficulty_meter/1, difficulty: 2)

      assert count_occurrences(html, "difficulty-bar-filled") == 2
      assert count_occurrences(html, "difficulty-bar-empty") == 3
    end

    test "D4 fills four of five bars without the red tint" do
      # #1519: the red "this is the expensive one" tint follows the flagship
      # level, which is now D5. D4 is premium/max — dear, but not opt-in.
      html = render_component(&difficulty_meter/1, difficulty: 4)

      assert count_occurrences(html, "difficulty-bar-filled") == 4
      assert count_occurrences(html, "difficulty-bar-empty") == 1
      refute html =~ "bg-error"
    end

    test "D5 fills all five bars and tints them red" do
      html = render_component(&difficulty_meter/1, difficulty: 5)

      assert count_occurrences(html, "difficulty-bar-filled") == 5
      assert count_occurrences(html, "difficulty-bar-empty") == 0
      assert html =~ "bg-error"
      assert html =~ "Difficulty D5"
    end

    test "only D5 tints red; lower difficulties use the neutral fill" do
      html = render_component(&difficulty_meter/1, difficulty: 3)

      refute html =~ "bg-error"
    end

    test "nil difficulty renders all five bars empty without a border" do
      html = render_component(&difficulty_meter/1, difficulty: nil)

      assert count_occurrences(html, "difficulty-bar-filled") == 0
      assert count_occurrences(html, "difficulty-bar-empty") == 5
      refute html =~ "border-dashed"
    end

    test "out-of-range or wrong-type difficulty degrades gracefully instead of crashing" do
      html = render_component(&difficulty_meter/1, difficulty: 6)
      assert count_occurrences(html, "difficulty-bar-filled") == 0
      assert count_occurrences(html, "difficulty-bar-empty") == 5
      assert html =~ "Difficulty: not set"
      refute html =~ "border-dashed"

      html = render_component(&difficulty_meter/1, difficulty: "3")
      assert count_occurrences(html, "difficulty-bar-filled") == 0
      assert count_occurrences(html, "difficulty-bar-empty") == 5
      refute html =~ "border-dashed"
    end
  end

  describe "type_tag/1" do
    test "renders the literal type value" do
      html = render_component(&type_tag/1, type: :bug_fix)
      assert html =~ "bug_fix"
    end

    test "accepts string types" do
      html = render_component(&type_tag/1, type: "spike")
      assert html =~ "spike"
    end
  end

  describe "data_list/1" do
    test "renders each item's label and value" do
      assigns = %{}

      html =
        rendered_to_string(~H"""
        <.data_list>
          <:item label="Priority">P1</:item>
          <:item label="Status">running</:item>
        </.data_list>
        """)

      assert html =~ "Priority"
      assert html =~ "P1"
      assert html =~ "Status"
      assert html =~ "running"
    end
  end

  describe "data_table/1" do
    test "renders a header per column and a cell per row" do
      assigns = %{rows: [%{name: "alpha"}, %{name: "beta"}]}

      html =
        rendered_to_string(~H"""
        <.data_table id="things" rows={@rows}>
          <:col :let={row} label="Name">{row.name}</:col>
        </.data_table>
        """)

      assert html =~ "Name"
      assert html =~ "alpha"
      assert html =~ "beta"
    end

    test "outer wrapper has overflow-x-auto for horizontal scrolling on mobile" do
      assigns = %{rows: [%{id: "task1"}, %{id: "task2"}]}

      html =
        rendered_to_string(~H"""
        <.data_table id="tasks" rows={@rows}>
          <:col :let={row} label="ID" width="84px">{row.id}</:col>
          <:col :let={row} label="Name">{row.id}</:col>
        </.data_table>
        """)

      # The outer wrapper should have overflow-x-auto for horizontal scrolling
      assert html =~ "overflow-x-auto"
    end

    test "min_width keeps columns from collapsing to zero on a narrow viewport" do
      assigns = %{rows: [%{id: "task1", detail: "open → in_progress"}]}

      html =
        rendered_to_string(~H"""
        <.data_table id="tasks" rows={@rows} min_width="600px">
          <:col :let={row} label="ID" width="84px">{row.id}</:col>
          <:col :let={row} label="Detail" width="minmax(200px, 1fr)" wrap>{row.detail}</:col>
        </.data_table>
        """)

      # Both the header row and the body rows must carry the min-width so the
      # grid tracks (in particular a `minmax(_, 1fr)` column) can't be forced
      # to zero by a container narrower than the content needs — that's what
      # squished the audit log's Detail column into one character per line.
      assert count_occurrences(html, "min-width: 600px") == 2
    end
  end

  defp count_occurrences(haystack, needle) do
    haystack
    |> String.split(needle)
    |> length()
    |> Kernel.-(1)
  end
end
