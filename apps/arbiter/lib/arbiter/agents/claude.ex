defmodule Arbiter.Agents.Claude do
  @moduledoc """
  Claude adapter implementing `Arbiter.Agents.Agent`.

  Wraps the existing `Arbiter.Worker.ClaudeSession` parsing pipeline behind
  the `Agent` behaviour. The session/parsing logic stays in
  `ClaudeSession` (callable as a library); this module is the seam that
  lets a future adapter (Codex / Aider / Gemini) replace just the argv +
  parser without forcing the worker to grow a switch statement.

  Phase B of `docs/agent-harness-design.md` — Claude is the only adapter
  today, intentionally. The two cheaper levers (model-tiering and
  multi-key rotation) ship inside *this* adapter rather than as new
  vendors:

    * `:model` opt routes through `claude --model <name>`. Resolution
      order is `opts[:model]` → `:model_tier` (resolved per-adapter via
      `Claude.Config.model_for_tier/1`) →
      `Arbiter.Agents.Claude.Config.active_model/0` → CLI default
      (no `--model` flag).
    * `:thinking` opt routes through the configured effort argv (default
      `--effort <level>` for low/medium/high; the
      cheap second lever the moduledoc has always called out). Resolved
      via `Claude.Config.thinking_argv/1`.
    * Multi-key rotation: when `api_keys` is set on the workspace, each
      session picks the next key via per-process round-robin and exports
      `ANTHROPIC_API_KEY` for the spawn — addresses rate-limit relief
      without new harness code.

  All three default off, so workspaces that haven't opted in see
  unchanged behavior.

  ## Security posture

  `default_argv/2` also bakes in the spawn's **security posture**. The
  caller threads a resolved `Arbiter.Agents.SecurityPolicy` in via
  `opts[:security]` (Dispatch / ReviewGate resolve it from the workspace);
  `Arbiter.Agents.Claude.Security` maps it to `--permission-mode` /
  `--dangerously-skip-permissions` + an inline `--settings` deny/allow
  document. A bare call with no `:security` opt falls back to the install-wide
  hardened default (`SecurityPolicy.default/0`) — so every spawn is
  safe-by-default and **none** inherits the operator's personal
  `~/.claude/settings.json`.
  """

  @behaviour Arbiter.Agents.Agent

  alias Arbiter.Agents.Claude.Config
  alias Arbiter.Agents.Claude.ConfigDir
  alias Arbiter.Agents.Claude.Security
  alias Arbiter.Agents.SecurityPolicy
  alias Arbiter.Worker.ClaudeSession

  @done_regex ~r/\barb done\b/

  # Linux enforces MAX_ARG_STRLEN = 131_072 bytes as a *per-argument* limit on
  # execve() (stricter than overall ARG_MAX). A prompt element that exceeds
  # this makes exec() fail with E2BIG (errno 7) *before the child ever runs* —
  # zero stdout, zero stderr, just an immediate exit(7). bd-11abk2: task.notes
  # accumulates the full transcript of every review round with no cap, so a
  # task with a couple of review rounds routinely exceeds this. Prompts over
  # the limit are written to a temp file and piped in via stdin instead of
  # being spliced into argv, mirroring the fix already applied to the
  # code-review diff-checking path (bd-dl49fo,
  # Arbiter.Workflows.CodeReview.Checks.invoke_via_stdin/3).
  @max_prompt_argv_bytes 131_072
  @prompt_tmp_prefix "arb_prompt_"

  @impl true
  def provider, do: "claude"

  @impl true
  def security_enforced?, do: true

  @doc """
  Claude's own permission layer (`Arbiter.Agents.Claude.Security` + the
  generated `CLAUDE_CONFIG_DIR` settings) is the confinement mechanism in
  every mode — there is no OS jail here (bd-1abj7u). Reported as
  `:permission_layer` regardless of `policy.permissions.mode`: it's the
  same settings document and deny baseline that make `security_enforced?/0`
  answer `true`.
  """
  @impl true
  def write_confinement(%SecurityPolicy{}), do: :permission_layer

  @impl true
  def done_sentinel, do: @done_regex

  @impl true
  def default_argv(prompt, opts \\ []) when is_binary(prompt) do
    case resolve_claude_executable() do
      {:ok, claude} ->
        policy = security_policy(opts)

        flags =
          model_flag(opts) ++
            thinking_flag(opts) ++
            Security.permission_argv(policy) ++
            Security.settings_argv(policy) ++
            mcp_config_flag(opts) ++
            stream_flags()

        build_argv(claude, prompt, flags)

      {:error, _} = err ->
        err
    end
  end

  # Build the `sh -c` wrapped streaming argv for a `claude --print` invocation.
  # Shared by the workspace-aware path (`default_argv/2` above) and the bare
  # `Arbiter.Worker.ClaudeSession.start/1` path so both get the E2BIG fix.
  #
  # Small prompts (the common case) are spliced into argv exactly as before —
  # `sh -c 'exec "$@" < /dev/null' sh <claude> --print <prompt> <flags...>`.
  #
  # Prompts over MAX_ARG_STRLEN are written to a temp file and delivered via
  # stdin instead: `sh -c 'f="$1"; shift; exec "$@" < "$f"' sh <tmpfile>
  # <claude> --print <flags...>` (no prompt element in argv at all — `claude
  # --print` with no positional prompt reads it from stdin). The temp file's
  # path is recoverable from argv[4] by `prompt_tmpfile/1` (named with
  # `@prompt_tmp_prefix` so that lookup can't misidentify an unrelated path)
  # so the worker can unlink it once the spawned session's port exits.
  @inline_prompt_script ~s(exec "$@" < /dev/null)
  @stdin_prompt_script ~s(f="$1"; shift; exec "$@" < "$f")

  @doc false
  def build_argv(claude, prompt, flags)
      when is_binary(claude) and is_binary(prompt) and is_list(flags) do
    if byte_size(prompt) > @max_prompt_argv_bytes do
      case write_prompt_tmpfile(prompt) do
        {:ok, tmp} ->
          {:ok, ["sh", "-c", @stdin_prompt_script, "sh", tmp, claude, "--print" | flags]}

        {:error, reason} ->
          {:error, {:prompt_tmpfile_failed, reason}}
      end
    else
      {:ok, ["sh", "-c", @inline_prompt_script, "sh", claude, "--print", prompt | flags]}
    end
  end

  defp write_prompt_tmpfile(prompt) do
    tmp =
      Path.join(
        System.tmp_dir!(),
        @prompt_tmp_prefix <> Integer.to_string(System.unique_integer([:positive])) <> ".txt"
      )

    case File.write(tmp, prompt) do
      :ok -> {:ok, tmp}
      {:error, _} = err -> err
    end
  end

  # Extract the stdin-delivery temp file path from an argv built by
  # `build_argv/3`, or `nil` when this argv used inline (mode A) delivery.
  # Used by the worker to unlink the file once the spawned port exits.
  @doc false
  def prompt_tmpfile(argv) when is_list(argv) do
    case Enum.at(argv, 4) do
      path when is_binary(path) -> if tmpfile_path?(path), do: path, else: nil
      _ -> nil
    end
  end

  # Splice `insert` (a list of argv elements, e.g. a swapped-in nudge/resume
  # prompt) right after the `--print` flag in `argv`, discarding whatever was
  # there before — the old inline prompt (mode A) or nothing at all (mode B,
  # stdin delivery, in which case the leading temp-file positional is also
  # dropped so the rebuilt argv is a plain mode-A invocation). Returns
  # `{:error, :no_print_slot}` when `argv` has no `--print` flag at all (test
  # fixtures / custom commands).
  @doc false
  def splice_prompt(argv, insert) when is_list(argv) and is_list(insert) do
    case Enum.find_index(argv, &(&1 == "--print")) do
      nil ->
        {:error, :no_print_slot}

      idx ->
        {head, [print | tail]} = Enum.split(argv, idx)

        case pop_tmpfile_positional(head) do
          {true, head} ->
            head = List.replace_at(head, 2, @inline_prompt_script)
            {:ok, head ++ [print] ++ insert ++ tail}

          {false, head} ->
            {:ok, head ++ [print] ++ insert ++ drop_first(tail)}
        end
    end
  end

  defp pop_tmpfile_positional(head) do
    case Enum.at(head, 4) do
      path when is_binary(path) ->
        if tmpfile_path?(path), do: {true, List.delete_at(head, 4)}, else: {false, head}

      _ ->
        {false, head}
    end
  end

  defp tmpfile_path?(path), do: Path.basename(path) |> String.starts_with?(@prompt_tmp_prefix)

  defp drop_first([_ | rest]), do: rest
  defp drop_first([]), do: []

  # The resolved `Arbiter.Agents.SecurityPolicy` for this spawn. Threaded in by
  # the caller (Dispatch / ReviewGate resolve it from the workspace); falls back to
  # the install-wide hardened default so a bare adapter call is still safe.
  defp security_policy(opts) do
    case Keyword.get(opts, :security) do
      %SecurityPolicy{} = policy -> policy
      _ -> SecurityPolicy.default()
    end
  end

  @impl true
  def auth_probe_argv(_opts \\ []) do
    # Cheapest token-validity probe: a one-word `claude --print` round-trip.
    # No streaming/model flags — we only care that the CLI authenticates. stdin
    # is closed via the sh wrapper (same as a real spawn) so the CLI doesn't
    # block waiting for piped input. An expired OAuth / bad key makes this print
    # "401 / invalid authentication credentials" and exit non-zero, which
    # Arbiter.Worker.StopReason classifies as :auth_expired.
    #
    # bd-adyhvn: `--output-format json` so the CLI reports what this round-trip
    # actually spent (~39K cache-read tokens a call, ~322 calls a day) and
    # `Arbiter.Agents.Preflight` can write it to the ledger. The flag changes
    # only the *success* output — an auth failure still prints its error text
    # and exits non-zero, and `Arbiter.Usage.Probe.parse/1` strips the success
    # payload before the classifier sees it so its integers can't be misread as
    # a `401`/`402` signature.
    case resolve_claude_executable() do
      {:ok, claude} ->
        {:ok,
         [
           "sh",
           "-c",
           ~s(exec "$@" < /dev/null),
           "sh",
           claude,
           "--print",
           "--output-format",
           "json",
           "ping"
         ]}

      {:error, _} = err ->
        err
    end
  end

  @impl true
  def spawn_env(opts \\ []) do
    # Worker runs get an isolated CLAUDE_CONFIG_DIR so the operator's personal
    # ~/.claude/CLAUDE.md (persona) can't bleed into the worker's context
    # (bd-3y2mda); `ConfigDir.env/1` also composes in the
    # CLAUDE_CODE_OAUTH_TOKEN (bd-2zigo1) when configured, so every
    # `ConfigDir`-based spawn path shares this single source and can't
    # diverge (bd-6umoh9). The optional API key composes on top.
    #
    # bd-bw3466: thread the spawn's workspace through so a token configured
    # the per-workspace way (`worker_env`, encrypted at rest) is found —
    # `ConfigDir.env/0` sees only the arbiter server's own process env, which
    # on a `worker_env` install is never where the token lives. Dispatch and
    # the ReviewGate both put `:workspace` on the adapter opts; a bare adapter
    # call (no workspace) takes the install-wide account credential with
    # `:provider_accounts_enabled` on, and falls back to the legacy chain
    # (server env, then install-wide-unambiguous workspace token) with it off
    # — kept per the operator's ruling on PR #1947 (P4, bd-cblemv round 2).
    ConfigDir.env(Keyword.get(opts, :workspace)) ++ api_key_env(opts)
  end

  defp api_key_env(opts) do
    case Keyword.get(opts, :api_key) || Config.resolve_api_key() do
      key when is_binary(key) and key != "" -> [{"ANTHROPIC_API_KEY", key}]
      _ -> []
    end
  end

  @impl true
  def init_session(_opts \\ []) do
    %{
      line_buf: "",
      output_lines: [],
      usage: %{},
      activity: nil,
      activity_at: nil
    }
  end

  @impl true
  def parse_line(session, line) when is_binary(line) do
    # The worker already accumulates lines; here we just turn one logical
    # line into display tuples + an updated session. Tool results carry the
    # "arm_done? = false" flag so a `cat`/`grep` of the literal `arb done`
    # phrase from disk can't trip the completion sentinel.
    next = ClaudeSession.handle_data(session, line <> "\n", true)
    display = collect_display(session, next)
    {display, next}
  end

  @impl true
  def async_tool_instruction do
    async_tool_instruction(
      "your VERDICT",
      "a VERDICT issued while a background task is still running is invalid,\n" <>
        "you would be judging on incomplete evidence",
      commit_first: false
    )
  end

  @impl true
  def async_tool_instruction(completion_signal, coda \\ nil, opts \\ []) do
    tail =
      case coda do
        nil -> "before you print #{completion_signal}."
        extra -> "before you print #{completion_signal} —\n#{extra}."
      end

    commit_bullet =
      if Keyword.get(opts, :commit_first, true) do
        """
          * COMMIT correct work BEFORE running any long verification. Verification
            confirms work; it must never be the thing that loses it.
        """
      else
        ""
      end

    """
    *** ASYNC TOOLS: THIS SESSION IS HEADLESS AND NON-INTERACTIVE: ending your
    turn ends the session outright, and no notification can ever reach you
    afterward — not from `Monitor`, not `ScheduleWakeup`, not a backgrounded
    shell job. The process that would receive it no longer exists. If you
    background a long command (`mix test`, `mix precommit`, `dialyzer`, or
    similar) and end your turn to "wait" for it, the run ends on the spot, the
    command is killed with it, and any uncommitted work is lost. So:

    #{commit_bullet}\
      * Run `mix test`, `mix precommit`, `dialyzer`, and any other long
        verification command in the FOREGROUND, in the same tool call, and
        wait for it to finish before your turn ends. Raise the `Bash` tool's
        own `timeout` parameter (up to 600000 ms / 10 minutes) if the default
        is too short, or narrow the command — the specific failing test
        files, not the whole suite.
      * NEVER background a verification command and end your turn expecting to
        be woken up later. NEVER call `Monitor` or `ScheduleWakeup` to wait for
        one. There is no "later" in a headless session.

    You MUST read every command's full output #{tail}\
    """
  end

  # bd-1zz5mn / bd-606zlr: the Claude CLI's OWN markers for "an asynchronous
  # wait is now armed" — text this Arbiter build did not write and the agent
  # did not choose the wording of. Emitted when a `Bash` call is backgrounded
  # (either up front or after blowing its tool timeout), when a `Monitor`
  # starts, or when a `ScheduleWakeup` is booked. Matching the CLI's phrasing
  # rather than the agent's prose ("I'll wait for the notification") is
  # deliberate: the prose is unbounded paraphrase, the markers are fixed
  # strings.
  @async_arm_signature ~r/
      you[ _]will[ _]be[ _]notified
    | moved[ _]to[ _]the[ _]background[ _]\(id:
    | running[ _]in[ _]the[ _]background[ _]with[ _]id:
    | command[ _]running[ _]in[ _]background[ _]with[ _]id:
    | monitor[ _]started[ _]\(task
    | wakeup[ _]scheduled
  /ix

  @impl true
  def async_arm_signature, do: @async_arm_signature

  @impl true
  def usage_attrs(session),
    do: ClaudeSession.usage_summary(session) |> Map.put(:provider, provider())

  # ---- Internals ---------------------------------------------------------

  # The session map updated by ClaudeSession.handle_data/3 contains an
  # :output_lines list (newest-first). Whatever lines were appended on this
  # call are the display tuples we owe the caller. We diff old vs new and
  # synthesize the {text, arm_done?} tuples.
  #
  # Today the worker lives outside the adapter and owns the buffered
  # state — so `parse_line/2` is only invoked from the adapter test surface
  # and from the agent-routing scaffolding. The ReviewGate/worker hot-path
  # still calls ClaudeSession directly. We keep the adapter parse_line
  # callable so future adapters can plug in without rewriting the worker.
  defp collect_display(prev, next) do
    prev_len = length(Map.get(prev, :output_lines, []))
    new_len = length(Map.get(next, :output_lines, []))

    added =
      next.output_lines
      |> Enum.take(new_len - prev_len)
      |> Enum.reverse()

    # All lines added through ClaudeSession.handle_data are display lines;
    # the only ones the worker treats as non-arming are tool-result lines,
    # which ClaudeSession's emit path already exempts from the done sentinel
    # via the `detect_done?` flag. We mirror that here as a best-effort:
    # lines starting with the tool-result glyph are not arming.
    Enum.map(added, fn line ->
      {line, !tool_result_line?(line)}
    end)
  end

  defp tool_result_line?(line) when is_binary(line),
    do: String.starts_with?(line, "⏴ ")

  defp tool_result_line?(_), do: false

  defp model_flag(opts) do
    case resolve_model(opts) do
      nil -> []
      model when is_binary(model) -> ["--model", model]
    end
  end

  defp resolve_model(opts) do
    case Keyword.get(opts, :model) do
      m when is_binary(m) and m != "" ->
        m

      _ ->
        case Keyword.get(opts, :model_tier) do
          tier when is_binary(tier) and tier != "" ->
            Config.model_for_tier(tier) || Config.active_model()

          _ ->
            Config.active_model()
        end
    end
  end

  defp thinking_flag(opts) do
    case Keyword.get(opts, :thinking) do
      level when is_binary(level) and level != "" -> Config.thinking_argv(level)
      _ -> []
    end
  end

  # bd-7e8ezw: name the spawn's injected `.mcp.json` explicitly. Claude Code
  # applies the MAIN checkout's `.claude/settings.local.json` to every git
  # worktree of the repo, and a `disabledMcpjsonServers: ["arbiter"]` there
  # silently drops the worktree's auto-loaded `.mcp.json` — the operator
  # declining the server for their own interactive session cut every worker
  # off from Arbiter's MCP tools. `--mcp-config` servers are not subject to
  # that list. Only set by callers that wrote the file themselves
  # (`Arbiter.Worker.Dispatch.inject_mcp_config/3`), never derived from
  # whatever `.mcp.json` happens to sit in the cwd: a review spawn runs in
  # the operator's shared checkout, whose own config is theirs to disable.
  defp mcp_config_flag(opts) do
    case Keyword.get(opts, :mcp_config) do
      path when is_binary(path) and path != "" -> ["--mcp-config", path]
      _ -> []
    end
  end

  defp stream_flags, do: ["--output-format", "stream-json", "--verbose"]

  defp resolve_claude_executable do
    case System.find_executable("claude") do
      nil -> {:error, {:executable_not_found, "claude"}}
      path -> {:ok, path}
    end
  end
end
